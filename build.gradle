buildscript {
    repositories {
        jcenter()
        mavenLocal()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

repositories {
    jcenter()
    mavenLocal()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven' // 'gradle install' task

android {
    compileSdkVersion 25
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 212
        versionName "1.0.135"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "android-audio-library"
version = android.defaultConfig.versionName

def prop(String name) {
    project.hasProperty(name) ? project.getProperty(name) : null
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: prop('ossrhUsername'), password: prop('ossrhPassword'))
            }

            pom.project {
                name 'Android Library'
                packaging 'jar'
                description 'Android Simple Widgets and Support classes.'
                url 'https://gitlab.com/axet/android-audio-library'

                scm {
                    connection 'scm:git:https://gitlab.com/axet/android-library'
                    developerConnection 'scm:git:https://gitlab.com/axet/android-library'
                    url 'https://gitlab.com/axet/android-audio-library'
                }

                licenses {
                    license {
                        name 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url 'http://www.gnu.org/copyleft/lesser.html'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.github.axet:android-library:1.27.46' // api project(':android-library')
    api 'com.android.support:design:25.3.1'
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.github.axet:jebml:0.0.2' // compile project(':jebml')
    api 'com.github.axet:vorbis:1.0.2'
    // debugCompile project(path:':android-vorbis', configuration:'libraryDebug')
    api 'com.github.axet:lame:1.0.3' // compile project(':android-lame')
    api 'com.github.axet:java-flac-encoder:0.3.8'
    api 'com.github.axet:opus:1.0.2' // compile project(':android-opus')
    api 'com.github.axet:jssrc:1.0.2-2'
    api 'org.gagravarr:vorbis-java-core:0.8'
    api 'uk.me.berndporr:iirj:1.0'
    testImplementation 'junit:junit:4.12'
}
