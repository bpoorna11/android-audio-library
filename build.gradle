buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

repositories {
    jcenter()
    mavenLocal()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven' // 'gradle install' task

android {
    compileSdkVersion 25
    buildToolsVersion "27.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 156
        versionName "1.0.80"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "android-audio-library"
version = android.defaultConfig.versionName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Android Library'
                packaging 'jar'
                description 'Android Simple Widgets and Support classes.'
                url 'https://gitlab.com/axet/android-audio-library'

                scm {
                    connection 'scm:git:https://gitlab.com/axet/android-library'
                    developerConnection 'scm:git:https://gitlab.com/axet/android-library'
                    url 'https://gitlab.com/axet/android-audio-library'
                }

                licenses {
                    license {
                        name 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url 'http://www.gnu.org/copyleft/lesser.html'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.github.axet:android-library:1.19.10' // compile project(':android-library')
    compile 'com.android.support:design:25.3.1'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.github.axet:jebml:0.0.2' // compile project(':jebml')
    compile 'com.github.axet:vorbis:1.0.2' // debugCompile project(path:':android-vorbis', configuration:'libraryDebug')
    compile 'com.github.axet:lame:1.0.3' // compile project(':android-lame')
    compile 'com.github.axet:java-flac-encoder:0.3.8'
    compile 'com.github.axet:opus:1.0.2' // compile project(':android-opus')
    compile 'com.github.axet:jssrc:1.0.2-1'
    compile 'org.gagravarr:vorbis-java-core:0.8'
    testCompile 'junit:junit:4.12'
}
